#compdef git-annex
#description:managing files while ignoring their content

# ZSH Completion for git-annex (http://git-annex.branchable.com/)
#
# This completion was written for git-annex version: 3.20111122~bpo60+2
#
# Note that this completion is still very much early alpha work in progress.
#
# This completion depends on Python for a jason parser, sorry. Unfortunately
# there is no such thing in zsh (yet).
#
# Skeleton implementation by Frank Terbeck <ft@bewatermyfriend.org>
# Blanks filled in by Valentin Haenel <valentin.haenel@gmx.de>
# Licence: WTFPL (http://sam.zoy.org/wtfpl/)
#
# To use this completion drop it somewhere in you $fpath, e.g.:
#
#     $ git clone $CLONEURL
#     $ fpath+=$PWD/git-annex-zsh-completion
#     $ compinit git-annex

local state line context
local -A opt_args

__annex_backend(){
    local -a backends
    backends=($(git annex status --fast --json |
    python -c "
import sys
import json
parsed = json.loads(sys.stdin.read())
print ' '.join(parsed['supported backends'])"))
    _describe -t backends backend backends
}

__annex_repository(){

    local -a currents
    currents=(\.)
    _describe -t currents current currents

    local -a descriptions
    descriptions=($(git annex status --fast --json |
    python -c "
import sys
import json
parsed = json.loads(sys.stdin.read())
descriptions = []
for repo in (parsed['trusted repositories']
        + parsed['semitrusted repositories']
        + parsed['untrusted repositories']):
    descriptions.append(repo['description'])
print ' '.join(descriptions)"))
    _describe -t descriptions description descriptions

    local -a remotes
    remotes=($(git remote))
    _describe -t remotes 'configured remote' remotes

    local -a uuids
    uuids=($(git annex status --fast --json |
    python -c "
import sys
import json
parsed = json.loads(sys.stdin.read())
uuids = []
for repo in (parsed['trusted repositories']
        + parsed['semitrusted repositories']
        + parsed['untrusted repositories']):
    uuids.append(repo['uuid'])
print ' '.join(uuids)"))
    _describe -t uuids uuid uuids
}

local -a arguments
arguments=(
    '--and[both previous and next option match]'
    '(-a --auto)'{-a,--auto}'[automatic mode]'
    '(-d --debug)'{-d,--debug}'[show debug messages]'
    '(-x --exclude=)'{-x,--exclude=}'[skips files matching the globbing pattern]'
    '(-b --backend=)'{-b,--backend=}'[specifies which key-value backend to use]::backend:__annex_backend'
    '-c name=-[override git configuration settings]'
    '(-C --copies=)'{-C,--copies=}'[skip files with fewer copies]'
    '--force[force unsafe actions]'
    '(-f --from=)'{-f,--from=}'[copy/move from repository]:repository:__annex_repository'
    '(-F --fast)'{-F,--fast}'[less expensive version of some commands]'
    '(-i --in=)'{-i,--in=}'[skip files not present in a remote]:repository:__annex_repository'
    '(-j --json)'{-j,--json}'[generate machine readable JSON]'
    '(-k --key=)'{-k,--key=}'[specifies a key to operate on]'
    '(-N --numcopies=)'{-N,--numcopies=}'[overrides annex.numcopies]'
    '--not[negate next option]'
    '--or[either previous or next option match]'
    '(-t --to=)'{-t,--to=}'[copy/move to repository]:repository:__annex_repository'
    '--semitrust=-[override trust settings]:repository:__annex_repository'
    '--trust=-[override trust settings]:repository:__annex_repository'
    '--untrust=-[override trust settings]:repository:__annex_repository'
    '--verbose[verbose display]'
    '--quiet'
    '-([open group of options]'
    '-)[close group of options]'
    ': :->command'
    '*:: :->subcmd'
)

_arguments -C $arguments

case $state in
(command)
    local -a cmds
    cmds=(
        add:'adds files in the path to the annex'
        addurl:'downloads each url to a file, which is added to the annex.'
        copy:'copy from/to repositories'
        drop:'drops the content of annexed files from this repository'
        dropkey:'drop annexd data for specified keys'
        dropunused:'drop unused data, as reported by unused'
        describe:'changes the description of a repository'
        edit:'alias for the unlock command'
        find:'search for present/missing files in current annex'
        fix:'fix up symlinks that have become broken'
        fromkey:'set file to link to given key'
        fsck:'check annex consistency'
        get:'makes the content of annexed files available in this repository'
        init:'initialize an annex'
        initremote:'set up a special remote'
        lock:'lock file again after editing'
        map:'generate a visual map of repositories'
        merge:'merge the git-annex branches of remotes'
        migrate:'migrate annexd files to new backend'
        move:'move files from/to repositories'
        pre-commit:'fixes up symlinks that are staged as part of a commit'
        semitrust:'returns a repository to the default semi trusted state'
        status:'displays some statistics and other information'
        setkey:'sets the annexed data for a key to the content of the specified file, and then removes the file.'
        trust:'records that a repository is trusted to not  unexpectedly  lose  content'
        unannex:'undo an accidental git annex add command'
        uninit:'stop using git-annex entirely'
        unlock:'unlock a file for editing'
        untrust:'records that a repository is not trusted and could lose content at any time'
        unused:'check for data that has no symlinks pointing to it'
        upgrade:'upgrades the repository to current layout'
        version:'print version and repository information'
        whereis:'show all repositories that contain a file'
    )
    _describe -t commands command cmds
    ;;
(subcmd)
    case ${line[1]} in
    (map|status)
        _message 'No more arguments'
        ;;
    (*)
        _arguments -C $arguments
        _path_files
        ;;
    esac
    ;;
esac

