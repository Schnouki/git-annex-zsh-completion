#compdef git-annex
#description:managing files while ignoring their content

local state line context
local -A opt_args

local -a arguments
arguments=(
    '--force[force something]'
    '--fast[don'\''t be slow]'
    '--verbose[tell me more]'
    ': :->command'
    '*:: :->subcmd'
)

_arguments -C $arguments

case $state in
(command)
    local -a cmds
    cmds=(
        add:'adds files in the path to the annex'
        drop:'drops the content of annexed files from this repository'
        describe:'changes the description of a repository'
        get:'makes  the  content of annexed files available in this repository'
        map:'generate a visual map of repositories'
        move:'move files from/to repositories'
        status:'displays some statistics and other information'
    )
    _describe -t commands command cmds
    ;;
(subcmd)
    case ${line[1]} in
    (map|status)
        _message 'No more arguments'
        ;;
    (*)
        _path_files
        ;;
    esac
    ;;
esac

